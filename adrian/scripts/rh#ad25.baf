// Initial goto
// ------------
IF
	NumTimesTalkedTo(0)
THEN
	RESPONSE #100
		Dialogue(Player1)
END

// Begin talks
// -----------
IF
	InParty("rh#Adrian")
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianTalksTOB","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#AdrianTalksTOBTimer","GLOBAL",1800)
		SetGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	Global("rh#AdrianTalksTOB","GLOBAL",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#AdrianTalksTOBTimer","GLOBAL",2700)
		SetGlobal("rh#AdrianTalksTOB","GLOBAL",3)
END

// Talks 1, 2, Saradush, 5, 6
// --------------------------
IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	OR(5)
		Global("rh#AdrianTalksTOB","GLOBAL",1)
		Global("rh#AdrianTalksTOB","GLOBAL",3)
		Global("rh#AdrianTalksTOB","GLOBAL",9)
		Global("rh#AdrianTalksTOB","GLOBAL",11)
		Global("rh#AdrianTalksTOB","GLOBAL",13)
THEN
	RESPONSE #100
		IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// Talk 1 (2) Friendship
// ---------------------
IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	Global("rh#AdrianTalksTOB","GLOBAL",3)
THEN
	RESPONSE #100
		IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// Talk 4
// ------
IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("AR4500")
	GlobalGT("chapter","GLOBAL",%bg2_chapter_8%)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianTalksTOB","GLOBAL",7)
THEN
	RESPONSE #100
		IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// Initiate talk, romance
// ----------------------
IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	OR(6)
		Global("rh#AdrianTalksTOB","GLOBAL",2)
		Global("rh#AdrianTalksTOB","GLOBAL",4)
		Global("rh#AdrianTalksTOB","GLOBAL",8)
		Global("rh#AdrianTalksTOB","GLOBAL",10)
		Global("rh#AdrianTalksTOB","GLOBAL",12)
		Global("rh#AdrianTalksTOB","GLOBAL",14)
THEN
	RESPONSE #100
		PlaySong(0)
		PlaySound("rh#adrom")
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Initiate talk, friendship
// -------------------------
IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	OR(2)
		Global("rh#AdrianTalksTOB","GLOBAL",4)
		Global("rh#AdrianTalksTOB","GLOBAL",8)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Wraith
// ------
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("rh#AdrianRomanceActive","GLOBAL",2)
    Global("rh#LovedOne","GLOBAL",1)
    !ActuallyInCombat()
    Global("rh#AdrianWraithTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianWraithTalk","GLOBAL",1)
END

IF
    InParty("rh#Adrian")
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !ActuallyInCombat()
    Global("rh#AdrianRomanceActive","GLOBAL",2)
    Global("rh#AdrianWraithTalk","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

// Oasis
// -----
IF
	InParty(Myself)
	See(Player1)
	Dead("amtgen01")
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	GlobalGT("TethyrBattleStart","GLOBAL",0)
	Global("rh#AdrianOasisTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianOasisTalk","GLOBAL",1)
		RealSetGlobalTimer("rh#AdrianTalksOasisTimer","GLOBAL",60)
END

IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianOasisTalk","GLOBAL",1)
	RealGlobalTimerExpired("rh#AdrianTalksOasisTimer","GLOBAL")
THEN
	RESPONSE #100
			SetGlobal("rh#AdrianOasisTalk","GLOBAL",2)
END

IF
	RealGlobalTimerExpired("rh#AdrianTalksOasisTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	CombatCounter(0)
	Global("rh#AdrianOasisTalk","GLOBAL",2)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Amkethran
// ---------
IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	GlobalGT("METBAL","GLOBAL",0)
	Global("rh#AdrianAmkethranTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianAmkethranTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianAmkethranTalk","GLOBAL",1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// 4th Challenge
// -------------
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("rh#CyricChallengeTalk","GLOBAL",1)
		RealSetGlobalTimer("rh#CyricChallengeTalkTimer","GLOBAL",10)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",1)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#CyricChallengeTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("rh#CyricChallengeTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",2)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#CyricChallengeTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Final talk
// ----------
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",0)
	!See([ENEMY])
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianFinalTalk","GLOBAL",1)
		RealSetGlobalTimer("rh#FinalTalkTimer","GLOBAL",30)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",1)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#FinalTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianFinalTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",2)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#FinalTalkTimer","GLOBAL")
THEN
	RESPONSE #100
		PlaySong(0)
		PlaySound("rh#adrom")
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Flirts
// ------
IF
	Global("rh#AdrStartFlirtsTOB","GLOBAL",0)
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrStartFlirtsTOB","GLOBAL",1)
		RealSetGlobalTimer("rh#AdrianFlirtTimer","GLOBAL",1800)
END

IF
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#AdrianFlirtTimer","GLOBAL")
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrTimeForFlirt","GLOBAL",0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrTimeForFlirt","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#AdrianFlirtTimer","GLOBAL")
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrTimeForFlirt","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

// Jaheira: 1, 2, 4
// ----------------
IF
	InParty("rh#Adrian")
	InParty("Jaheira")
	Global("rh#AdrianJaheiraTOB","GLOBAL",0)
	!Alignment("rh#Adrian",MASK_EVIL)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#AdrianJaheiraTOBTalkTimer","GLOBAL",3600)
		SetGlobal("rh#AdrianJaheiraTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	InParty("Jaheira")
	See("Jaheira")
	!StateCheck("rh#Adrian",CD_STATE_NOTVALID)
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#AdrianJaheiraTOBTalkTimer","GLOBAL")
	OR(3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",1)
		Global("rh#AdrianJaheiraTOB","GLOBAL",3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",7)
THEN
	RESPONSE #100
	IncrementGlobal("rh#AdrianJaheiraTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	InParty("Jaheira")
	See("Jaheira")
	!StateCheck("rh#Adrian",CD_STATE_NOTVALID)
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#AdrianJaheiraTOBTalkTimer","GLOBAL")
	OR(3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",2)
		Global("rh#AdrianJaheiraTOB","GLOBAL",4)
		Global("rh#AdrianJaheiraTOB","GLOBAL",8)
THEN
	RESPONSE #100
	StartDialogueNoSet("Jaheira")
END

// Sarevok
// -------
IF
	InParty("rh#Adrian")
	InParty("Sarevok")
	GlobalGT("LS#SarevokSex","GLOBAL",2)
	Global("rh#AdrianSarevokConflict","GLOBAL",0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		RealSetGlobalTimer("rh#AdrianSarevokConflictTimer","GLOBAL",600)
		SetGlobal("rh#AdrianSarevokConflict","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	InParty("Sarevok")
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianSarevokConflict","GLOBAL",1)
	RealGlobalTimerExpired("rh#AdrianSarevokConflictTimer","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianSarevokConflict","GLOBAL",2)
END

IF
	InParty("rh#Adrian")
	InParty("Sarevok")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianSarevokConflict","GLOBAL",2)
	RealGlobalTimerExpired("rh#AdrianSarevokConflictTimer","GLOBAL")
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// Irenicus
// --------
IF
    InParty(Myself)
    InParty("lrirenic")
    Global("rh#AdrianIrenicusTOBTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("rh#AdrianIrenicusTOBTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    Global("rh#AdrianIrenicusTOBTalk","GLOBAL",1)
THEN
	RESPONSE #100
		MoveToObject(Player1)
		StartDialogueNoSet(Player1)
END

// String fixer
// ------------
IF
	HotKey(K)
THEN
	RESPONSE #100
		SetName(~Adrian~)
        SetPlayerSound(Myself,@1004,MORALE)				// ~Oh, I don't think so.~ [rh#ad1]
        SetPlayerSound(Myself,@1005,HAPPY)				// ~I must admit, this is rather effective.~ [rh#ad2]
        SetPlayerSound(Myself,@1006,UNHAPPY_ANNOYED)	// ~Well, we certainly know how to waste our time.~ [rh#ad3]
        SetPlayerSound(Myself,@1007,UNHAPPY_SERIOUS)	// ~Is this sort of nonsense really necessary?~ [rh#ad4]
        SetPlayerSound(Myself,@1008,LEADER)				// ~Oh, I don't mind this at all.~ [rh#ad5]
        SetPlayerSound(Myself,@1009,TIRED)				// ~What is this, a Thayvian plantation? Couldn't we find an inn?~[rh#ad6]
        SetPlayerSound(Myself,@1010,BORED)				// ~If I'm drunk again by the time you decide to do something, you've only yourself to blame.~ [rh#ad7]
        SetPlayerSound(Myself,@1011,DAMAGE)				// ~Damn!~ [rh#ad8]
        SetPlayerSound(Myself,@1012,DYING)				// ~No... not like this...~ [rh#ad9]
        SetPlayerSound(Myself,@1013,HURT)				// ~Where's a damn cleric when I need one?~ [rh#ad10]
        SetPlayerSound(Myself,@1014,AREA_FOREST)		// ~It's not exactly Cormanthor, is it?~ [rh#ad11]
        SetPlayerSound(Myself,@1015,AREA_CITY)			// ~Ah, the sights and smells of the city... I could do without the latter.~ [rh#ad12]
        SetPlayerSound(Myself,@1016,AREA_DUNGEON)		// ~Quite the setting... I'm beginning to wish we'd simply paid someone to do this for us.~ [rh#ad13]
        SetPlayerSound(Myself,@1017,AREA_DAY)			// ~A lovely day, isn't it? Shame about the locale.~ [rh#ad14]
        SetPlayerSound(Myself,@1018,AREA_NIGHT)			// ~Mmm... the sun sets, and the day truly begins.~ [rh#ad15]
        SetPlayerSound(Myself,@1019,CRITICAL_HIT)		// ~That should just about finish this.~ [rh#ad16]
        SetPlayerSound(Myself,@1020,CRITICAL_MISS)		// ~Dammit!~ [rh#ad17]
        SetPlayerSound(Myself,@1021,TARGET_IMMUNE)		// ~Oh, this is working wonderfully. If you don't find me a better weapon soon, I might as well just sit in a corner and watch.~ [rh#ad18]
        SetPlayerSound(Myself,@1022,INVENTORY_FULL)		// ~I'm a mage, not a pack mule.~ [rh#ad19]
        SetPlayerSound(Myself,@1023,SPELL_DISRUPTED)	// ~Some cover over here, if you don't mind...~ [rh#ad20]
        SetPlayerSound(Myself,@1024,SET_A_TRAP)			// ~This is sure to be entertaining.~ [rh#ad21]
        SetPlayerSound(Myself,@1025,HIDDEN_IN_SHADOWS)	// ~It's not quite invisibility, but I suppose it'll do.~ [rh#ad22]
        SetPlayerSound(Myself,@1026,PICKED_POCKET)		// ~Well, what have we here?~ [rh#ad23]
        SetPlayerSound(Myself,@1027,BATTLE_CRY1)		// ~To the Abyss with you!~ [rh#ad24]
        SetPlayerSound(Myself,@1028,BATTLE_CRY2)		// ~No mercy for scum like this!~ [rh#ad25]
        SetPlayerSound(Myself,@1029,BATTLE_CRY3)		// ~Die, already!~ [rh#ad26]
        SetPlayerSound(Myself,@1030,BATTLE_CRY4)		// ~I can already count the gold.~ [rh#ad27]
        SetPlayerSound(Myself,@1031,BATTLE_CRY5)		// ~Don't even bother to run.~ [rh#ad28]
        SetPlayerSound(Myself,@1032,SELECT_COMMON1)		// ~Yes?~ [rh#ad29]
        SetPlayerSound(Myself,@1033,SELECT_COMMON2)		// ~What is it?~ [rh#ad30]
        SetPlayerSound(Myself,@1034,SELECT_COMMON3)		// ~Speak up.~ [rh#ad31]
        SetPlayerSound(Myself,@1035,SELECT_COMMON4)		// ~What now?~ [rh#ad32]
        SetPlayerSound(Myself,@1036,SELECT_COMMON5)		// ~Spit it out already.~ [rh#ad33]
        SetPlayerSound(Myself,@1037,SELECT_COMMON6)		// ~Hmm?~ [rh#ad34]
        SetPlayerSound(Myself,@1039,SELECT_ACTION1)		// ~Certainly.~ [rh#ad35]
        SetPlayerSound(Myself,@1039,SELECT_ACTION2)		// ~Of course.~ [rh#ad36]
        SetPlayerSound(Myself,@1040,SELECT_ACTION3)		// ~If you insist. And you always do, don't you?~ [rh#ad37]
        SetPlayerSound(Myself,@1041,SELECT_ACTION4)		// ~A waste of a mage's talents, if you ask me.~ [rh#ad38]
        SetPlayerSound(Myself,@1042,SELECT_ACTION5)		// ~Yes, yes, yes. I'm working on it.~ [rh#ad39]
        SetPlayerSound(Myself,@1043,SELECT_ACTION6)		// ~Oh, not this again.~ [rh#ad40]
        SetPlayerSound(Myself,@1044,SELECT_ACTION7)		// ~You can do better than that.~ [rh#ad41]
        SetPlayerSound(Myself,@1045,SELECT_RARE1)		// ~Go point that thing at someone else.~ [rh#ad42]
        SetPlayerSound(Myself,@1046,SELECT_RARE2)		// ~Well, you do have a way with words, don't you?~ [rh#ad43]
END
