// Initial goto
IF
    NumTimesTalkedTo(0)
THEN
  RESPONSE #100
    Dialogue(Player1)
END

// begin talks

IF
	InParty("rh#Adrian")
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianTalksTOB","GLOBAL",0)
THEN
RESPONSE #100
	RealSetGlobalTimer("rh#AdrianTalksTOBTimer","GLOBAL",1800)
	SetGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	Global("rh#AdrianTalksTOB","GLOBAL",0)
THEN
RESPONSE #100
	RealSetGlobalTimer("rh#AdrianTalksTOBTimer","GLOBAL",2700)
	SetGlobal("rh#AdrianTalksTOB","GLOBAL",3)
END

// talks 1, 2, saradush, 5, 6

IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	OR(5)
		Global("rh#AdrianTalksTOB","GLOBAL",1)
		Global("rh#AdrianTalksTOB","GLOBAL",3)
		Global("rh#AdrianTalksTOB","GLOBAL",9)
		Global("rh#AdrianTalksTOB","GLOBAL",11)
		Global("rh#AdrianTalksTOB","GLOBAL",13)
THEN
RESPONSE #100
	IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// talk 1 (2) friendship

IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	Global("rh#AdrianTalksTOB","GLOBAL",3)
THEN
RESPONSE #100
	IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// talk 4

IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("AR4500")
	GlobalGT("chapter","GLOBAL",%bg2_chapter_8%)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianTalksTOB","GLOBAL",7)
THEN
RESPONSE #100
	IncrementGlobal("rh#AdrianTalksTOB","GLOBAL",1)
END

// initiate talk, romance

IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	OR(6)
		Global("rh#AdrianTalksTOB","GLOBAL",2)
		Global("rh#AdrianTalksTOB","GLOBAL",4)
		Global("rh#AdrianTalksTOB","GLOBAL",8)
		Global("rh#AdrianTalksTOB","GLOBAL",10)
		Global("rh#AdrianTalksTOB","GLOBAL",12)
		Global("rh#AdrianTalksTOB","GLOBAL",14)
 THEN
RESPONSE #100
	PlaySong(0)
    PlaySound("rh#adrom")
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// initiate talk, friendship

IF
	RealGlobalTimerExpired("rh#AdrianTalksTOBTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianFriendshipActive","GLOBAL",1)
	OR(2)
		Global("rh#AdrianTalksTOB","GLOBAL",4)
		Global("rh#AdrianTalksTOB","GLOBAL",8)
 THEN
RESPONSE #100	
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// wraith

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("rh#AdrianRomanceActive","GLOBAL",2)
    Global("rh#LovedOne","GLOBAL",1)
    !ActuallyInCombat()
    Global("rh#AdrianWraithTalk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("rh#AdrianWraithTalk","GLOBAL",1)
END

IF
    InParty("rh#Adrian")
    See(Player1)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !ActuallyInCombat()
    Global("rh#AdrianRomanceActive","GLOBAL",2)
    Global("rh#AdrianWraithTalk","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

// oasis

IF
	InParty(Myself)
	See(Player1)
	Dead("amtgen01")
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	GlobalGT("TethyrBattleStart","GLOBAL",0)
	Global("rh#AdrianOasisTalk","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("rh#AdrianOasisTalk","GLOBAL",1)
	RealSetGlobalTimer("rh#AdrianTalksOasisTimer","GLOBAL",60)
END

IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianOasisTalk","GLOBAL",1)
	RealGlobalTimerExpired("rh#AdrianTalksOasisTimer","GLOBAL")
THEN
RESPONSE #100
	SetGlobal("rh#AdrianOasisTalk","GLOBAL",2)
END

IF
	RealGlobalTimerExpired("rh#AdrianTalksOasisTimer","GLOBAL")
	InParty("rh#Adrian")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	CombatCounter(0)
	Global("rh#AdrianOasisTalk","GLOBAL",2)
 THEN
RESPONSE #100	
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// amkethran

IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	GlobalGT("METBAL","GLOBAL",0)
	Global("rh#AdrianAmkethranTalk","GLOBAL",0)
THEN
RESPONSE #100
	SetGlobal("rh#AdrianAmkethranTalk","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	OR(2)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianAmkethranTalk","GLOBAL",1)
THEN
RESPONSE #100
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// 4th Challenge

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",0)
	!See([ENEMY])
THEN
RESPONSE #100
	SetGlobal("rh#CyricChallengeTalk","GLOBAL",1)
	RealSetGlobalTimer("rh#CyricChallengeTalkTimer","GLOBAL",10)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",1)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#CyricChallengeTalkTimer","GLOBAL")
THEN
RESPONSE #100
SetGlobal("rh#CyricChallengeTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	OR(2)
		Global("rh#AdrianFriendshipActive","GLOBAL",1)
		Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("BeginChallenge4","GLOBAL",6)
	Global("rh#CyricChallengeTalk","GLOBAL",2)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#CyricChallengeTalkTimer","GLOBAL")
THEN
RESPONSE #100
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// final talk

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",0)
	!See([ENEMY])
THEN
RESPONSE #100
	SetGlobal("rh#AdrianFinalTalk","GLOBAL",1)
	RealSetGlobalTimer("rh#FinalTalkTimer","GLOBAL",30)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",1)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#FinalTalkTimer","GLOBAL")
THEN
RESPONSE #100
	SetGlobal("rh#AdrianFinalTalk","GLOBAL",2)
END

IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("ChallengesDone","GLOBAL",1)
	Global("rh#AdrianFinalTalk","GLOBAL",2)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#FinalTalkTimer","GLOBAL")
THEN
RESPONSE #100
	PlaySong(0)
    PlaySound("rh#adrom")	
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END

// flirts

IF 
	Global("rh#AdrStartFlirtsTOB","GLOBAL",0)
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
 THEN
RESPONSE #100
	SetGlobal("rh#AdrStartFlirtsTOB","GLOBAL",1)
	RealSetGlobalTimer("rh#AdrianFlirtTimer","GLOBAL",1800)
END

IF
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#AdrianFlirtTimer","GLOBAL")
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrTimeForFlirt","GLOBAL",0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
 THEN
RESPONSE #100	
	SetGlobal("rh#AdrTimeForFlirt","GLOBAL",1)
END

IF
	InParty("rh#Adrian")
	See(Player1)
	!ActuallyInCombat()
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	RealGlobalTimerExpired("rh#AdrianFlirtTimer","GLOBAL")
	Global("rh#AdrianFlirtsEnabled","GLOBAL",1)
	Global("rh#AdrTimeForFlirt","GLOBAL",1)
 THEN
RESPONSE #100
	StartDialogueNoSet(Player1)
END

// Jaheira: 1, 2, 4

IF
	InParty("rh#Adrian") 
	InParty("Jaheira")
	Global("rh#AdrianJaheiraTOB","GLOBAL",0)
	!Alignment("rh#Adrian",MASK_EVIL)
THEN
RESPONSE #100
	RealSetGlobalTimer("rh#AdrianJaheiraTOBTalkTimer","GLOBAL",3600)
	SetGlobal("rh#AdrianJaheiraTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian") 
	InParty("Jaheira")
	See("Jaheira")
	!StateCheck("rh#Adrian",CD_STATE_NOTVALID)
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#AdrianJaheiraTOBTalkTimer","GLOBAL")
	OR(3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",1)
		Global("rh#AdrianJaheiraTOB","GLOBAL",3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",7)
THEN
RESPONSE #100
	IncrementGlobal("rh#AdrianJaheiraTOB","GLOBAL",1)
END

IF
	InParty("rh#Adrian") 
	InParty("Jaheira")
	See("Jaheira")
	!StateCheck("rh#Adrian",CD_STATE_NOTVALID)
	!StateCheck("Jaheira",CD_STATE_NOTVALID)
	CombatCounter(0)
	!See([ENEMY])
	RealGlobalTimerExpired("rh#AdrianJaheiraTOBTalkTimer","GLOBAL")
	OR(3)
		Global("rh#AdrianJaheiraTOB","GLOBAL",2)
		Global("rh#AdrianJaheiraTOB","GLOBAL",4)
		Global("rh#AdrianJaheiraTOB","GLOBAL",8)
THEN
RESPONSE #100
	StartDialogueNoSet("Jaheira")
END

// sarevok

IF
	InParty("rh#Adrian") 
	InParty("Sarevok")
	GlobalGT("LS#SarevokSex","GLOBAL",2) 
	Global("rh#AdrianSarevokConflict","GLOBAL",0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
	RealSetGlobalTimer("rh#AdrianSarevokConflictTimer","GLOBAL",600)
	SetGlobal("rh#AdrianSarevokConflict","GLOBAL",1)
END

IF
	InParty(Myself)
	See(Player1)
	InParty("Sarevok")
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianSarevokConflict","GLOBAL",1)
	RealGlobalTimerExpired("rh#AdrianSarevokConflictTimer","GLOBAL")
THEN
RESPONSE #100
	SetGlobal("rh#AdrianSarevokConflict","GLOBAL",2)
END

IF
	InParty("rh#Adrian")
	InParty("Sarevok")
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	CombatCounter(0)
	Global("rh#AdrianRomanceActive","GLOBAL",2)
	Global("rh#AdrianSarevokConflict","GLOBAL",2)
	RealGlobalTimerExpired("rh#AdrianSarevokConflictTimer","GLOBAL")
 THEN
RESPONSE #100	
	MoveToObject(Player1)
	StartDialogueNoSet(Player1)
END


// irenicus

IF
    InParty(Myself)
    InParty("lrirenic")
    Global("rh#AdrianIrenicusTOBTalk","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("rh#AdrianIrenicusTOBTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !Detect([ENEMY])
    Global("rh#AdrianIrenicusTOBTalk","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END

// string fixer

IF 
    HotKey(K)  
THEN 
    RESPONSE #100       
        SetName(~Adrian~) 
        SetPlayerSound(Myself,~Oh, I don't think so.~ [rh#ad1],MORALE) 
        SetPlayerSound(Myself,~I must admit, this is rather effective.~ [rh#ad2],HAPPY) 
        SetPlayerSound(Myself,~Well, we certainly know how to waste our time.~ [rh#ad3],UNHAPPY_ANNOYED)
        SetPlayerSound(Myself,~Is this sort of nonsense really necessary?~ [rh#ad4],UNHAPPY_SERIOUS)
        SetPlayerSound(Myself,~Oh, I don't mind this at all.~ [rh#ad5],LEADER)
        SetPlayerSound(Myself,~What is this, a Thayvian plantation? Couldn't we find an inn?~[rh#ad6],TIRED)
        SetPlayerSound(Myself,~If I'm drunk again by the time you decide to do something, you've only yourself to blame.~ [rh#ad7],BORED)
        SetPlayerSound(Myself,~Damn!~ [rh#ad8],DAMAGE)
        SetPlayerSound(Myself,~No... not like this...~ [rh#ad9],DYING)
        SetPlayerSound(Myself,~Where's a damn cleric when I need one?~ [rh#ad10],HURT)
        SetPlayerSound(Myself,~It's not exactly Cormanthor, is it?~ [rh#ad11],AREA_FOREST)
        SetPlayerSound(Myself,~Ah, the sights and smells of the city... I could do without the latter.~ [rh#ad12],AREA_CITY)
        SetPlayerSound(Myself,~Quite the setting... I'm beginning to wish we'd simply paid someone to do this for us.~ [rh#ad13],AREA_DUNGEON)
        SetPlayerSound(Myself,~A lovely day, isn't it? Shame about the locale.~ [rh#ad14],AREA_DAY)
        SetPlayerSound(Myself,~Mmm... the sun sets, and the day truly begins.~ [rh#ad15],AREA_NIGHT)
        SetPlayerSound(Myself,~That should just about finish this.~ [rh#ad16],CRITICAL_HIT)
        SetPlayerSound(Myself,~Dammit!~ [rh#ad17],CRITICAL_MISS)
        SetPlayerSound(Myself,~Oh, this is working wonderfully. If you don't find me a better weapon soon, I might as well just sit in a corner and watch.~ [rh#ad18],TARGET_IMMUNE)
        SetPlayerSound(Myself,~I'm a mage, not a pack mule.~ [rh#ad19],INVENTORY_FULL)
        SetPlayerSound(Myself,~Some cover over here, if you don't mind...~ [rh#ad20],SPELL_DISRUPTED)
        SetPlayerSound(Myself,~This is sure to be entertaining.~ [rh#ad21],SET_A_TRAP)
        SetPlayerSound(Myself,~It's not quite invisibility, but I suppose it'll do.~ [rh#ad22],HIDDEN_IN_SHADOWS)
        SetPlayerSound(Myself,~Well, what have we here?~ [rh#ad23],PICKED_POCKET)
        SetPlayerSound(Myself,~To the Abyss with you!~ [rh#ad24],BATTLE_CRY1) 
        SetPlayerSound(Myself,~No mercy for scum like this!~ [rh#ad25],BATTLE_CRY2) 
        SetPlayerSound(Myself,~Die, already!~ [rh#ad26],BATTLE_CRY3) 
        SetPlayerSound(Myself,~I can already count the gold.~ [rh#ad27],BATTLE_CRY4) 
        SetPlayerSound(Myself,~Don't even bother to run.~ [rh#ad28],BATTLE_CRY5)   
        SetPlayerSound(Myself,~Yes?~ [rh#ad29],SELECT_COMMON1)
        SetPlayerSound(Myself,~What is it?~ [rh#ad30],SELECT_COMMON2)
        SetPlayerSound(Myself,~Speak up.~ [rh#ad31],SELECT_COMMON3)
        SetPlayerSound(Myself,~What now?~ [rh#ad32],SELECT_COMMON4)
        SetPlayerSound(Myself,~Spit it out already.~ [rh#ad33],SELECT_COMMON5)
        SetPlayerSound(Myself,~Hmm?~ [rh#ad34],SELECT_COMMON6)
        SetPlayerSound(Myself,~Certainly.~ [rh#ad35],SELECT_ACTION1)
        SetPlayerSound(Myself,~Of course.~ [rh#ad36],SELECT_ACTION2)
        SetPlayerSound(Myself,~If you insist. And you always do, don't you?~ [rh#ad37],SELECT_ACTION3)
        SetPlayerSound(Myself,~A waste of a mage's talents, if you ask me.~ [rh#ad38],SELECT_ACTION4)
        SetPlayerSound(Myself,~Yes, yes, yes. I'm working on it.~ [rh#ad39],SELECT_ACTION5)
        SetPlayerSound(Myself,~Oh, not this again.~ [rh#ad40],SELECT_ACTION6) 
        SetPlayerSound(Myself,~You can do better than that.~ [rh#ad41],SELECT_ACTION7) 
        SetPlayerSound(Myself,~Go point that thing at someone else.~ [rh#ad42],SELECT_RARE1) 
        SetPlayerSound(Myself,~Well, you do have a way with words, don't you?~ [rh#ad43],SELECT_RARE2)
END